- num_layers = 3

.tile_editor
  .screen
    - num_layers.times do
      .layer
        .placed

    .selection

  .layer_select
    .choice.active Background
    .choice Midground
    .choice Foreground

  .tiles
    - Collection.find(params[:tileset_id]).sprites.each do |sprite|
      = image_tag(sprite.image.url, :alt => sprite.display_name)

:coffeescript
  resolution = 32

  currentLayer = 0

  modeDown = null

  prevTile = (mode) ->
    tileCount = $(".tiles img").length

    cur = $(".tiles ." + mode).removeClass(mode).index()

    $(".tiles img").eq((cur - 1).mod(tileCount)).addClass(mode)

  nextTile = (mode) ->
    tileCount = $(".tiles img").length

    cur = $(".tiles ." + mode).removeClass(mode).index()

    $(".tiles img").eq((cur + 1).mod(tileCount)).addClass(mode)

  entered = (x, y) ->
    if mode = modeDown
      posString = x + "x" + y

      element = $(".tiles").find("." + mode).clone().removeClass("primary secondary").css(
        position: "absolute"
        top: y
        left: x
      ).attr("data-pos", posString)

      targetLayer = $(".screen .layer").eq(currentLayer).find(".placed")

      targetLayer.find("[data-pos='" + posString + "']").remove()

      targetLayer.append(element)

  clickMode = (event) ->
    if event.which == 1
      "primary"
    else if event.which == 3
      "secondary"

  $(".tile_editor").bind "contextmenu", (event) ->
    event.preventDefault()

  $(".tiles img").live 'mousedown', (event) ->
    event.preventDefault()

    if mode = clickMode event
      $(this).addClass(mode).siblings().removeClass(mode)

  $(".layer_select .choice").live 'mousedown', (event) ->
    $(this).addClass("active").siblings().removeClass("active")

    currentLayer = $(this).index()

  $(".screen").bind "mousemove", (event) ->
    offset = $(this).offset()

    localY = (event.pageY - offset.top).snap(resolution)
    localX = (event.pageX - offset.left).snap(resolution)

    oldPos = $(".screen .selection").position()

    unless oldPos.left == localX && oldPos.top == localY
      entered(localX, localY)

      $(".screen .selection").css
        left: localX
        top: localY

  $(".screen").bind "mousedown", (event) ->
    if modeDown = clickMode event
      offset = $(this).offset()

      localY = (event.pageY - offset.top).snap(resolution)
      localX = (event.pageX - offset.left).snap(resolution)

      entered(localX, localY)

  $(document).bind "mouseup", (event) ->
    modeDown = null

  hotkeys =
    a: (event) ->
      prevTile("primary")
    z: (event) ->
      nextTile("primary")
    s: (event) ->
      prevTile("secondary")
    x: (event) ->
      nextTile("secondary")

  $.each hotkeys, (key, fn) ->
    $(document).bind "keydown", key, fn


%style
  :sass
    @import util

    .tile_editor
      .layer_select
        cursor: pointer

        .choice.active
          background-color: yellow
          width: 75px
          font-weight: bold

      .screen
        background-color: #ccc
        height: 322px
        overflow: hidden
        position: relative
        width: 482px

        .layer
          height: 320px
          left: 0
          position: absolute
          top: 0
          width: 480px

        .selection
          border: 1px solid green
          height: 32px
          position: absolute
          width: 32px

      img
        +inline-block

        margin-bottom: 2px
        padding: 1px

        &.primary, &.secondary
          border: 1px solid
          padding: 0px

        &.primary
          border-color: green
        &.secondary
          border-color: blue